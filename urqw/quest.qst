:start
music_volume=255
play_volume=255
music_looped=0
m=1
t=3
pln Приключения Сары Фаерман: Темная тайна темного хлева
btn 1, Начать игру
btn authors, Авторы
end

:1
invkill
inv+ Католический крестик надет

pln  Была уже поздняя ночь, когда в двери фермы у дороги постучали.
play sound/knock.wav
anykey
pln  - Кого там принесла нелегкая? - раздался ворчливый голос хозяина.
music voice/farmer/1.ogg
anykey
music stop
pln  Дверь со скрипом растворилась, и в тусклом свете маслянной лампы появился
_ усатый и вполне упитанный мужчина с хитрыми глазами и пышными усами.
play sound/door1.wav
anykey
pln  - Боже благослови сей дом, - сказала ему гостья, молодая девушка,
_ можно даже сказать девочка, с не вполне еще оформившейся грудью, укрытой 
_ впрочем от любопытных глаз за суровой монашеской рясой. 
music voice/sara/1.ogg
anykey
music stop
pln  Надо сказать, что девушке, а особенно особе столь юной, а тем более невесте
_ Христовой, в те трудные времена, когда в каждом углу пряталось зло, и долгими 
_ холодными ночами по подворотням луркали миньоны тьмы, не свойственно было
_ ходить одной, и одно это уже вызвало сей интерес. Впрочем вернемся к нашей
_ истории, а к миньонам вернемся позже, благо многочисленные возможности для 
_ этого нам еще предоставятся. Так вот.
anykey
pln  - Боже благослови сей дом, - сказала девица. - Не позволите ли бедной 
_ монахине, которую ночь застала в дороге, переночевать в вашем дома, и, - тут
_ у девицы словно нарошно заурчало в животе, и она густо покраснела, - и 
_ не дадите ли напиться, а то у меня с самого рассвета во рту росинки маковой
_ не было. Кстати, меня зовут Сара Фаерман.
music voice/sara/2.ogg
anykey
music stop
pln  - Да ты случаем не экзорцистка? - подозрительно спросил хозяин.
music voice/farmer/1-2.ogg
anykey
music stop
pln  - Мне всего четырнадцать, - отвечала монахиня, - но в будущем...
music voice/sara/3.ogg
anykey
music stop
pln  - Ба, да ты то мне и нужна, - закричал хозяин не дожидаясь ответа, и так 
_ хлопнул ее по спине, что у нее перед глазами поплыло, и потащил ее внутрь.
music voice/farmer/1-3.ogg
anykey
music stop
btn 2, Дальше
end

:2
pln  Усадив девицу за простой деревенский стол, хозяин налил себе в кружку пива,
_ и залпом ее осушил. 
play sound/drink.mp3
anykey
pln  - Слушай же, экзорцистка, героиня, спасительница нашей жизни, - начал он
_ отпускать комлименты, засмущав в конец юную монашку. - Только ты можешь помочь
_ нам, благослови Бог тот день, когда ночь застала тебя на дороге... Кстати,
_ хочешь пива?
music voice/farmer/2.ogg
anykey
music stop
pln  - Пост же, - заметила Сара Фаерман. - В пост никак нельзя.
music voice/sara/4.ogg
anykey
music stop
pln  Наступило неловкое молчание.
btn 4, Рассказать хозяину о себе
btn 3, Так что там у вас за проблема?
btn death1, Ладно, давайте ваше пиво
end

:3
pln  Хозяин закатил глаза к самому потолку и начал замогильным голосом:
pln  - Уже несколько дней странные существа луркают по моей ферме, пугая скот
_ и детей, что плохо для моих бизнесов. Хуже того, вчера вечером мой сын, 
_ пошел в хлев посетить корову, но из-за стога с сеном, неожиданно вылуркнуло
_ адово создание, укусило его за бедро, и скрылось за стогом. Мы думаем, что
_ оно до сих пор там и вооружилось вилами, и боимся идти в хлев. Я думал завтра
_ же бежать за экзорцистами, но, к счастью, Бог сам направил стопы великой
_ экзорцистки из самого монастыря к моему хлеву, прознав видимо о моих 
_ проблемах.
music voice/farmer/3-1.ogg
anykey
music stop
pln  - Но мне всего четырнадцать... - попыталась возразить Сара.
music voice/sara/5.ogg
anykey
music stop
pln  - А разве Давид был старше, когда победил Голиафа? - парировал хозяин. - К
_ тому же думаю ты хочешь сегодня поужинать и заночевать в тепле, а не на 
_ улице, где бродят бандиты и насильники?
music voice/farmer/3-2(2).ogg
anykey
music stop
btn 5, Ну разве я, Сара Фаерман, могу не помочь страждущим?
btn death1, Дай мне для смелости пива
end


:4
pln  - Меня зовут Сара Фаерман, - начала девушка. - В младенчестве меня подкинули
_ к воротам монастыря в Сент-Этьене. Я не знаю своих родителей, этот нательный
_ крестик, все что лежало в моей колыбельке. Все четырнадцать лет своей жизни
_ я провела в монастыре, вдали от мужчин, изучая богословие. Теперь я же я иду
_ проведать старика Амвросия, что живет отшельником в горах, но сбилась с пути,
_ и кривая дорожка вывела меня прямо к вашей ферме когда потемнело.
music voice/sara/6.ogg
anykey
music stop
pln  - Благословен будь тот час, когда она тебя сюда вывела, - сказал хозяин,
_ потирая руки.
music voice/farmer/4.ogg
anykey
music stop
btn 3, Так что там у вас за проблема?
btn death1, Ладно, давайте ваше пиво
end

:death1
pln  Сара Фаерман выпила кружку пива, тут же забыла обо всем и потеряла 
_ контроль над собой и ситуацией. Утром она проснулась уже беременной, на завтра
_ ее отчислили из монастыря, через год она стала падшей женщиной в Ницце, а 
_ через пять лет, армии миньонов поработили весь мир. Вот что случается, когда
_ кто-то, забыв про обеты, пьет пиво в пост!
play sound/drink.wav
pause 2000
play sound/burp.wav
pln
pln  Приключения Сары Фаерман на этом закончились.
pln
pln  КОНЦОВКА 1: ЗАБЫТЫЕ ОБЕТЫ
goto relive
end

:death2
pln  Как только Сара Фаерман потушила фонарь, вокруг воцарилась ТЬМА. И из
_ ТЬМЫ выскочил миньон тьмы, и разорвал тело Сары своими когтистыми пальцами.
_ А чему удивляться, если ей было всего четырнадцать?
play sound/yell.mp3
pln
pln  КОНЦОВКА 2: СМЕРТЬ ВО ТЬМЕ
goto relive
end

:death3
pln  Сара Фаерман бросила на крышу фонарь. Соломенная крыша занялась мгновенно,
_ через секунду вспыхнул и весь хлев. Неожиданно, прямо на Сару, из раскрытых
_ ворот хлева выскочило вооруженное вилами существо, и скоро воткнул их
_ прямо Саре в живот. Последнее, что запомнила Сара, был крик фермера "Я разорен!
_ Разорен!" и сатанинский хохот миньона тьмы за его спиной.
music voice/farmer/death3(2).ogg
anykey
music stop
pln
pln  КОНЦОВКА 3: СМЕРТЬ У ОГНЯ
goto relive
end

:death4
play sound/fire.mp3
pln  Сара Фаерман бросила на крышу фонарь. Соломенная крыша занялась мгновенно,
_ через секунду вспыхнул и весь хлев. Сара стояла и любовалась пожаром, пока
_ из дому не выбежал фермер.
anykey
pln  - Вот он, настоящий экзорцизм! - с гордостью сказала Сара.
music voice/sara/7.ogg
anykey
music stop
pln  - Мои коровы, мои свиньи, мой хлев! Прости меня, Господи, - сказал фермер,
_ вскидывая арбалет.
music voice/farmer/death4(2).ogg
anykey
music stop
pln  Последнее, что запомнила Сара, была пронзившая ее горло стрела.
pln
pln  КОНЦОВКА 5/ПОБЕДА 1: ВСЕОЧИЩАЮЩЕЕ ПЛАМЯ 
goto relive
end

:death5
pln  Ах, Сара, не все то золото что блестит! Сара попыталась разглядеть, что же
_ блестит на дне колодца, но подскользнулась и упала вниз, на самое его дно!
pln
play sound/yell.mp3
pln  КОНЦОВКА 4: ГУБИТ ЛЮДЕЙ НЕ ПИВО
goto relive
end

:relive
forget_procs
pln  Но Сара Фаерман никогда не сдается!
quit
end


:5
pln  - Ну вот и славненько, - сказал хозяин, и проводил Сару во двор. - Хлев вон
_ там. Стучись, когда справишся. Держи фонарь, в хлеву темно.
music voice/farmer/5.ogg
anykey
music stop
pln  Сказав это, он хлопнул и дверью, и быстро закрыл ее на засов изнутри.
pln
play sound/door2.wav
pln  Сара Фаерман получила Маслянный Фонарь.
pln
inv+ Маслянный Фонарь
goto main

:main
loc=0
pln  Сара Фаерман во дворе фермы. Двери и ставни фермы наглухо заперты. К 
_ востоку расположен страшный темный хлев. Прямо посреди двора находится колодец. 
btn farm, Идти к ферме
btn hlev, Идти к хлеву
btn well, Идти к колодцу
goto buttons
end

:farm
loc=1
pln  - Откройте, прошу вас, - стучала Сара Фаерман в двери фермы. - Мне не по
_ силам победить Миньона Тьмы, мне всего четырнадцать лет.
music voice/sara/8.ogg
anykey
music stop
pln  - Если вера твоя будет сильна, - ответил из-за двери хозяин. - Ты сможешь
_ победить и тысячу!
music voice/farmer/farm(2).ogg
anykey
music stop
btn main, Отойти от двери
goto buttons
end

:hlev
loc=2
pln  Сара стоит перед хлевом, в котором по слухам должен находиться адский миньон. Во дворе, несмотря на позднее время, все еще по августовски жарко, а крыша амбара соломенная, и может загореться от любой искры.
if closed=0 then pln  Ворота хлева открыты и зовут внутрь, вот тьму.
if closed=1 then pln  Ворота хлева наглухо закрыты.
if closed=0 then btn close, Закрыть ворота
if closed=1 then btn open, Открыть ворота
btn inside, Войти в хлев
btn main, Отойти от хлева
goto buttons
end

:open
if Католический крестик в руке then pln С крестом в руке этого не сделаешь.&btn hlev, Дальше&end
pln  Сара Фареман открыла ворота хлева.
play sound/door1.wav
closed=0
btn hlev, Дальше
end

:close
if Католический крестик в руке then pln С крестом в руке этого не сделаешь.&btn hlev, Дальше&end
pln  Сара Фареман закрыла ворота хлева.
play sound/door2.wav
closed=1
btn hlev, Дальше
end

:well
loc=3
pln  Сара Фаерман подошла к колодцу. На дне колодца что-то блестит.
if fill=0 then pln  Неподалеку стоит пустое ведро.
if fill=1 then pln  У колодца стоит ведро полное воды.
btn death5, Выяснить что блестит на дне колодца
if fill=0 then btn bucket, Наполнить ведро водой
btn main, Отойти от колодца
goto buttons
end

:bucket
if Католический крестик в руке then pln С крестом в руке этого не сделаешь.&btn hlev, Дальше&end
fill=1
pln  Сара опустила ведро в колодец, и наполнила его водой.
play sound/fill.wav
btn back, Дальше
end

:buttons
if Маслянный Фонарь and loc<>2 then btn death2, Маслянный Фонарь
if Маслянный Фонарь and loc=2 and closed=0 then btn death3, Маслянный Фонарь
if Маслянный Фонарь and loc=2 and closed=1 then btn death4, Маслянный Фонарь
if Католический крестик надет then btn takeoff, Католический крестик
if Католический крестик в руке then btn wear, Католический крестик
if Ведро Святой Воды then btn empty, Ведро Святой Воды
btn pray, Молиться 
end

:empty
pln  Не стоит выливать Святую Воду на землю. Она еще может пригодиться.
btn back, Дальше
end

:pray
save #%current_loc$
play sound/holy.wav
pln  Чувственные губы Сары Фаерман произнесли молитву Господу. Девушка сразу же
_ почувствовала духовный подъем.
if loc=3 and fill=1 and Католический крестик в руке then pln  Сара опустила
_ крест в воду, и вода заискрилась голубым сиянием.&pln&pln  Сара Фаерман получает
_ Ведро Святой Воды.&inv+ Ведро Святой Воды&fill=2&play sound/holy.mp3
if loc=1 then anykey&pln  - Никакие молитвы не заставят меня открыть тебе двери, - 
_ раздался из-за двери голос хозяина. - Лучше просто пойди и прибей этого 
_ миньона в хлеву.&music voice/farmer/pray.ogg
btn back, Дальше
end

:back
if loc=0 then goto main
if loc=1 then goto farm
if loc=2 then goto hlev
if loc=3 then goto well
end

:takeoff
pln  Сара Фаерман сняла с шеи крестик, и плотно зажала его в руке.
inv- Католический крестик надет
inv+ Католический крестик в руке
btn back, Дальше
end

:wear
pln  Сара Фаерман поцеловала крестик, и надела его себе на шею.
inv+ Католический крестик надет
inv- Католический крестик в руке
play sound/kisses.wav
btn back, Дальше
end

:inside
if closed=1 then closed=0&pln  [Предварительно открыв ворота хлева.]
pln  - Успокойся, Сара, - сказала себе юная монашка. - Миньон Тьмы боится тебя
_ сильней чем ты его.
music voice/sara/9.ogg
anykey
music stop
pln  В хлеву блеяли козы, мычали коровы, молчали свиньи.
play sound/animals.wav
anykey
if Католический крестик надет then pln  Неожиданно из-за стога с сеном выскочил
_ Миньон Тьмы с вилами в руках, и страшно заорал. Сара закричала в ответ и 
_ быстро выбежала из хлева.& pln  - Нет, все-таки я боюсь сильнее, - сказала
_ девушка.&music voice/sara/10.ogg&anykey&music stop
if Католический крестик в руке then pln Неожиданно из-за стога с сеном выскочил
_ Миньон Тьмы с вилами в руках, и страшно заорал. Сара выставила руку с крестом
_ вперед, и закричала в ответ. Ситуация была патовая, Сара боялась вил, существо
_ боялось креста. "Тут бы ведерко Святой Воды", подумала Сара.&play sound/yell.mp3&pause 500&play sound/yell.mp3&pause 500&music voice/sara/11.ogg&anykey&music stop

if Ведро Святой Воды then btn holy, Ведро Святой Воды else btn hlev, Дальше
end

:holy
pln  Сара Фаерман вылила Ведро Святой Воды на Миньона Тьмы. С адовым криком он
_ взорвался! 
play sound/fill.wav
pause 1000
play sound/yell.mp3
anykey
pln  - Вот молодец, - раздался сзади голос хозяина фермы. - А говорила не 
_ экзорцист! Я знал, что ты справишься!
music voice/farmer/holy.ogg
anykey
music stop
pln  Так Сара убила своего первого миньона. И это было приятно.
pln
pln  ПОБЕДА 2: ЧИСТАЯ ПОБЕДА
btn finish, Дальше
end

:finish
pln  Но приключения Сары Фаерман на этом не заканчиваются.
pln
pln  В следующей серии - Сара Фаерман и Жуткая тайна Жуткого Тракта:
pln  На пути к старику Амвросию Саре предстоит пересечь Жуткий Тракт, жуткое место.
_ По слухам там водится около трех Миньонов Тьмы. Сможет ли Сара, вооруженная
_ лишь простым католическим крестиком и силой молитвы победить их всех? 
pln
pln  КОНЕЦ
end

:authors
textalign=3
play sound/fire.mp3
cls
pln&pln&pln&pln&pln&pln&pln&pln&pln&pln
pln  Джордж Павленко представляет
pause 2000
cls
pln&pln&pln&pln&pln&pln&pln&pln&pln&pln
pln Аджента Эрроу в роли Сары Фаерман
pause 4000
cls
pln&pln&pln&pln&pln&pln&pln&pln&pln&pln
pln Евгений Бычков в роли мерзкого латышского фермера
pause 4000
cls
textalign=1
goto start
